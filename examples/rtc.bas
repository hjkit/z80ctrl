10 REM CONVERT VALUES FROM BCD
20 DEF FNBCD(V)=(V AND &HF0)/16*10+(V AND &HF)
30 INPUT "DS1306+ or DS1302+ (enter 6 or 2)"; CH
40 IF CH=6 THEN RY=6:RM=5:RD=4:RH=2:RN=1:RS=0: GOTO 70
50 IF CH=2 THEN RY=&H8D:RM=&H89:RD=&H87:RH=&H85:RN=&H83:RS=&H81: GOTO 70
60 GOTO 30
70 OUT 0,0: REM SELECT RTC CHIP
80 REM READ RTC REGISTERS
90 OUT 1,RY: YR=FNBCD(INP(2))+2000
100 OUT 1,RM: MO=FNBCD(INP(2))
110 OUT 1,RD: DY=FNBCD(INP(2))
120 OUT 1,RH: HR=INP(2)
130 OUT 1,RN: MI=FNBCD(INP(2))
140 OUT 1,RS: S=FNBCD(INP(2))
150 REM HANDLE AM/PM
160 AMPM$=""
170 IF (HR AND &H40)=0 THEN 200: REM SKIP IF 24-HR FORMAT
180 IF HR AND &H20 THEN AMPM$="PM" ELSE AMPM$="AM"
190 HR=HR AND &H1F: REM GET RID OF AM/PM AND 12-HR BITS
200 HR=FNBCD(HR)
210 PRINT USING "The time is ##:##:##& on ##/##/####"; HR, MI, S, AMPM$, MO, DY, YR