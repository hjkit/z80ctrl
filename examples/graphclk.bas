10 REM GRAPHICAL CLOCK FOR MSX-BASIC
20 FG=15: BG=4: REM FOREGROUND/BACKGROUND COLORS
30 CX=128: CY=96: R=95: REM CENTER AND RADIUS
40 SL=R*0.72: ML=R*0.60: HL=R*0.50: REM HAND LENGTHS
50 TE=R*0.95: TS=R*0.90: TL=R*0.80: REM TICK LENGTHS

100 REM ONE-TIME SETUP
100 S=99: M=99: H=99
110 SX=CX: SY=CY: MX=CX: MY=CY: HX=CX: HY=CY
120 OUT 0,0: REM SELECT RTC CHIP
130 DEF FNBCD(V)=(V AND &HF0)/16*10+(V AND &HF): REM BCD TO BINARY
140 INPUT "DS1306+ or DS1302+ (enter 6 or 2)"; CT
150 IF CT=6 THEN RH=2:RM=1:RS=0: GOTO 180
160 IF CT=2 THEN RH=&H85:RM=&H83:RS=&H81: GOTO 180
170 GOTO 140
180 GOSUB 700

200 REM MAIN LOOP
210 GOSUB 300
220 IF INKEY$="" THEN 210
230 END

300 REM UPDATE SECOND HAND
310 LS=S: OUT 1,RS: S=FNBCD(INP(2))
320 IF S=LS THEN RETURN
330 LX=SX: LY=SY
340 SA=S/60*6.28-1.57: SX=CX+COS(SA)*SL: SY=CY+SIN(SA)*SL
350 GOSUB 600

400 REM UPDATE MINUTE HAND
410 LM=M: OUT 1,RM: M=FNBCD(INP(2))
420 IF M=LM THEN RETURN
430 LX=MX: LY=MY
440 MA=M/60*6.28-1.57: MX=CX+COS(MA)*ML: MY=CY+SIN(MA)*ML
450 GOSUB 600

500 REM UPDATE HOUR HAND
510 LH=H: OUT 1,RH: H=INP(2)
520 IF (H AND &H40) THEN H=H AND &H1F: REM GET RID OF AM/PM BITS
530 H=FNBCD(H)
540 IF H>12 THEN H=H-12
550 IF H=LH THEN RETURN
560 LX=HX: LY=HY
570 HA=H/12*6.28-1.57: HX=CX+COS(HA)*HL: HY=CY+SIN(HA)*HL
580 GOSUB 600
590 RETURN

600 REM DRAW CLOCK HANDS
610 COLOR BG
620 LINE(CX,CY)-(LX,LY)
630 COLOR FG
640 LINE(CX,CY)-(SX,SY)
650 LINE(CX,CY)-(MX,MY)
660 LINE(CX,CY)-(HX,HY)
670 RETURN

700 REM DRAW CLOCK FACE
710 SCREEN 2: COLOR FG,BG,BG: CLS
720 CIRCLE(CX,CY),R
730 FOR H = 1 TO 24
740 HA=H/24*6.28-1.57: CH=COS(HA): SH=SIN(HA)
750 IF H MOD 2 THEN TB=TS ELSE TB=TL
760 LINE(CX+CH*TB,CY+SH*TB)-(CX+CH*TE,CY+SH*TE)
770 NEXT
780 RETURN